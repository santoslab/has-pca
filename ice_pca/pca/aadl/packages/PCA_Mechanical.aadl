--PCA_Mechanical.aadl

--holds mechanical components

package PCA_Mechanical
  public
  with 
    Base_Types,
    Physical_Types,
    PCA_Types, 
--    PCA_Properties,
    PCA_Pump;


--this is the button provided to patients to request more painkiller
device patient_button
  features
--MONITORED VARIABLE
    Request_Bolus: out event port;  --patient has pressed the button requesting a bolus
--EXTERNAL PHENOMENON
    Button_Press: in event port;  --physically depressing the button
end patient_button;

device implementation patient_button.imp
--button passes along third-party presses

end patient_button.imp;

--switch to stand alone mode 
device stand_alone_switch
  features
--MONITORED VARIABLE
    Stand_Alone: out data port;  --clinician disables ICE interface
--EXTERNAL PHENOMENON
    Switch_Position: in data port;  --physical position of the switch
end stand_alone_switch;

device implementation stand_alone_switch.imp
--stand-alone switch is error free
end stand_alone_switch.imp;
 
device drug_reservoir
  features
  --internal bus
    i_bus: requires bus access PCA_Pump::Internal_Bus;
  --drug flows out, but rate comes in
    Drug_Outlet : out data port Physical_Types::Fluid_Flow;  --drug from reservoir
    Fill_Reservoir : in data port Physical_Types::Fluid_Volume;  --put drug in reservoir
    Low_Reservoir: out event port;  --warning: little drug left
    Empty_Reservoir: out event port;  --alarm: (almost) no drug left
    Door_Open: out data port Base_Types::Boolean;  --reservoir door is open
    Door_Closed: out event port;  --door was closed
  flows
    Flow_dns_out : flow source Drug_Outlet;  --consumer of rate information
end drug_reservoir;

device implementation drug_reservoir.imp
 
end drug_reservoir.imp; 

--this is the type for the physical pump that move the drug
device pump
  features
  --internal bus
    i_bus: requires bus access PCA_Pump::Internal_Bus;
    Drug_Outlet : out data port Physical_Types::Fluid_Flow;  --drug to downstream monitor
    Drug_Intake : in data port Physical_Types::Fluid_Flow;   --drug from upstream monitor   
    Halt: in event port; 
    Rate: in data port PCA_Types::Flow_Rate;   --rate to pump
    Pump_Too_Hot: out event port;        --pump hot alarm
  flows
    Flow_pump_in : flow sink Drug_Intake;
    Flow_pump_out : flow source Drug_Outlet;
    Flow_pump_path : flow path Drug_Intake->Drug_Outlet;
end pump;

--this is the implementation of the pysical pump
--note that the actual fluid flow is not modeled; only its electrical/control aspects
device implementation pump.imp

end pump.imp;

--this is the device type for the fluid monitor between the reservoir and the pump
device upstream_monitor
  features
  --internal bus
    i_bus: requires bus access PCA_Pump::Internal_Bus;
    Drug_Outlet : out data port Physical_Types::Fluid_Flow;  --drug to physical pump
    Drug_Intake : in data port Physical_Types::Fluid_Flow;   --drug from reservoir
    -- Low Value Error -
    Upstream_Flow_Rate: out data port PCA_Types::Flow_Rate;  --measured drug flow rate
    -- Low Value Error -- 
    Occlusion: out event port;  --upstream occlusion detected
  flows
    Flow_ups_in : flow sink Drug_Intake;
    Flow_ups_out : flow source Drug_Outlet;
    Flow_ups_path : flow path Drug_Intake->Drug_Outlet;
end upstream_monitor;

--this is the implementation of the fluid monitor between the reservoir and the pump
device implementation upstream_monitor.imp

end upstream_monitor.imp;

--this is the device type for the fluid monitor between the pump and the patient
device downstream_monitor
  features
  --internal bus
    i_bus: requires bus access PCA_Pump::Internal_Bus;
    Drug_Outlet : out data port Physical_Types::Fluid_Flow;  --drug to patient
    Drug_Intake : in data port Physical_Types::Fluid_Flow;   --drug from physical pump
    Downstream_Flow_Rate: out data port PCA_Types::Flow_Rate;  --measured drug flow rate
    Occlusion: out event port;		--downstream blockage detected
    Bubble: out event port;	    --bubble detected in line
  flows
    Flow_dns_in : flow sink Drug_Intake;
    Flow_dns_out : flow source Drug_Outlet;
    Flow_dns_path : flow path Drug_Intake->Drug_Outlet;
end downstream_monitor;

--this is the implementation of the fluid monitor between the pump and the patient
device implementation downstream_monitor.imp

end downstream_monitor.imp;


--this represents an optical or RFID scanner to read
--drug labels, patient wristbands, and clinician badges
device scanner
  features
    Do_Scan: in event port;                    --tell scanner to scan
    Scan_Done: out event port;                   --scan completed
    Scan_Data: out event data port PCA_Types::Scan_Data_Type;  --data from scanner
  --internal bus
    i_bus: requires bus access PCA_Pump::Internal_Bus;
end scanner;

device implementation scanner.imp
 
end scanner.imp;


end PCA_Mechanical;
